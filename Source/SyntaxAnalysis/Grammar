unit           -> block | if | while | line | function_definiton | class_definition | import

block          -> "{" unit* "}"
line           -> (assignment | expression) ";"

if             -> "if" expression block ("else" (if | block))?
while          -> "while" expression block

type           -> Identifier
union_type_hint -> type ( "|" type)*
optional_type_hint -> "[" union_type_hint "]"
type_hint      -> union_type_hint | optional_type_hint
type_conversion -> expression "as" type

assignment     -> assignable (":" type_hint)? "=" expression
assignable     -> Identifier | attr

expression     -> comparison
comparison     -> addition ( ( "!=" | "==" | ">" | ">=" | "<" | "<=" ) addition )?
addition       -> multiplication ( ( "-" | "+" ) multiplication )*
multiplication -> unary ( ( "/" | "*" ) unary )*
unary          -> ( "!" | "-" ) unary | primary
primary        -> Literal | Identifier | ( "(" expression ")" ) | call | attr | type_conversion

call           -> expression "(" call_argument_list? ")"
call_argument_list -> expression ("," expression)*

attr           -> expression "." Identifier

return         -> "return" expression? ";"
function_unit  -> block | if | while | line | function_definiton | return  // 在__parse_unit实现
function_block -> "{" function_unit* "}"                                   // 在__parse_block实现

function_definition -> "fn" Identifier "(" argument_list_definition? ")" ("->" type_hint)? function_block
argument_list_definition -> (Identifier (":" type_hint)? ) ("," (Identifier (":" type_hint)? ))*

attr_assignment -> AccessibilityModifier? assignment  // 在__parse_line实现
method_line    -> (attr_assignment | expression) ";"  // 在__parse_line实现
method_unit    -> block | if | while | method_line | function_definiton | return  // 在__parse_unit实现
method_block   -> "{" method_unit* "}"

method_definition -> "fn" Identifier "(" argument_list_definition? ")" ("->" type_hint)? method_block
class_definition -> "class" Identifier "{" (method_definition | configuration_command)* "}"

configuration_command -> "#" Identifier ":" Literal

imported_module -> Identifier ("." Identifier)
import         -> "import" imported_module